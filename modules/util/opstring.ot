/* -*- Mode: c++; tab-width: 4; c-basic-offset: 4; coding: iso-8859-1 -*-
 *
 * Copyright (C) 2002-2011 Opera Software ASA.  All rights reserved.
 *
 * This file is part of the Opera web browser.
 * It may not be distributed under any circumstances.
 */
group "util.opstring";

include "modules/encodings/decoders/utf8-decoder.h";
include "modules/encodings/encoders/utf8-encoder.h";

global
{
	int testlen;
	uni_char* teststring;
	uni_char* lesserstring;
	uni_char* shortstring;
	uni_char* shortdiffstring;

	uni_char* acteststring;
	uni_char* aclesserstring;
	uni_char* acshortstring;
	uni_char* acshortdiffstring;
}

setup
{
	testlen = 60;

	teststring = OP_NEWA(uni_char, testlen + 1);
	lesserstring = OP_NEWA(uni_char, testlen + 1);
	shortstring = OP_NEWA(uni_char, 11);
	shortdiffstring = OP_NEWA(uni_char, 11);
	acteststring = OP_NEWA(uni_char, testlen+1);
	aclesserstring = OP_NEWA(uni_char, testlen+1);
	acshortstring = OP_NEWA(uni_char, 11);
	acshortdiffstring = OP_NEWA(uni_char, 11);

	/* '\0345' = &aring; '\0305' = &Aring; '\0344' = &auml; '\0304' = &Auml; '\0247' = &sect;
	 * The use of exactly three octal digits is crucial: octal escapes are at most
	 * 3 digits, hex ones are arbitrarily long, so would eat subsequent [a-fA-F0-9] ! */
	uni_strcpy(teststring,    UNI_L("4yiuwefcfsada\345w384q23432\345ar\344a\344duq+48qw\247ho+8\345a\344a+3w0123456789"));
	uni_strcpy(acteststring,  UNI_L("4YIUWEFCFSADA\305W384Q23432\305AR\304A\304DUQ+48QW\247ho+8\305A\304A+3W0123456789"));

	uni_strcpy(lesserstring,  UNI_L("4yiuwefcfsada\345w384q23432\345ar\344a\344duq+48qw\247ho+8\345a\344a+3w0123416789"));
	uni_strcpy(aclesserstring,UNI_L("4YIUWEFCFSADA\305W384Q23432\305AR\304A\304DUQ+48QW\247ho+8\305A\304A+3W0123416789"));
	//                                                                                                           ^

	uni_strcpy(shortstring,       UNI_L("4yiuwefcfs"));
	uni_strcpy(acshortstring,     UNI_L("4YIUWEFCFS"));

	uni_strcpy(shortdiffstring,   UNI_L("3yiuwefcft"));
	uni_strcpy(acshortdiffstring, UNI_L("3YIUWEFCFT"));
	//                                   ^        ^
}

exit
{
  OP_DELETEA(teststring);
  OP_DELETEA(lesserstring);
  OP_DELETEA(shortstring);
  OP_DELETEA(shortdiffstring);

  OP_DELETEA(acteststring);
  OP_DELETEA(aclesserstring);
  OP_DELETEA(acshortstring);
  OP_DELETEA(acshortdiffstring);
}

test("Constructor (C)")
{
  OpStringC teststr(teststring);
  verify( teststr.CStr() == teststring );
}

test("Constructor (S)")
{
  OpStringS teststr;
  teststr.Set(teststring);
  const uni_char* strptr = teststr.DataPtr();

  verify(strptr != NULL);
  verify(strptr != teststring);

  for (int i = 0; i < testlen; i++)
  {
    verify(strptr[i] == teststring[i]);
  }
}

test( "Index (C)" )
{
    OpStringC teststr(teststring);
    for (int i = 0; i < testlen; i++)
      verify(teststr[i] == teststring[i]);
}

test( "Length (C)" )
{
    OpStringC teststr(teststring);
    verify(teststr.Length() == testlen);
}

test( "Compare (C)" )
{
    OpStringC teststr(teststring);
    OpStringC lesserstr(lesserstring);
    OpStringC shortstr(shortstring);

    verify(teststr.Compare(teststring) == 0);
    verify(teststr.Compare(lesserstring) > 0);
    verify(lesserstr.Compare(teststring) < 0);
    verify(shortstr.Compare(lesserstring, 10) == 0);
    verify(teststr.Compare(shortstring) > 0);
    verify(shortstr.Compare(teststring) < 0);

    verify(teststr.Compare(teststr) == 0);
    verify(teststr.Compare(lesserstr) > 0);
    verify(lesserstr.Compare(teststr) < 0);
    verify(teststr.Compare(shortstr) > 0);
    verify(shortstr.Compare(teststr) < 0);
}

test( "Compare case insensitive (C)" )
	require initialization;
{
    OpStringC teststr(teststring);
    OpStringC lesserstr(lesserstring);
    OpStringC shortstr(shortstring);
    OpStringC acteststr(acteststring);
    OpStringC aclesserstr(aclesserstring);
    OpStringC acshortstr(acshortstring);

    verify(teststr.CompareI(acteststring) == 0);
    verify(teststr.CompareI(aclesserstring) > 0);
    verify(lesserstr.CompareI(acteststring) < 0);
    verify(shortstr.CompareI(aclesserstring, 10) == 0);

    verify(teststr.CompareI(acshortstring) > 0);
    verify(shortstr.CompareI(acteststring) < 0);

    verify(teststr.CompareI(acteststr) == 0);
    verify(teststr.CompareI(aclesserstr) > 0);
    verify(lesserstr.CompareI(acteststr) < 0);
    verify(teststr.CompareI(acshortstr) > 0);
    verify(shortstr.CompareI(acteststr) < 0);
}

test("CStr (C)")
{
    OpStringC teststr(teststring);
    verify( !TestUtils::compareStr( teststring, (uni_char *)teststr.CStr() ) );
}

test("Find (C)")
{
    OpStringC teststr(teststring);
    OpStringC lesserstr(lesserstring);

    verify(teststr.Find(teststring) == 0);
    verify(teststr.Find(&teststring[10]) == 10);

    verify(teststr.Find(lesserstring) == KNotFound);
}

test("Find case insensitive (C)")
	require initialization;
{
    OpStringC teststr(teststring);

    verify(teststr.FindI(acteststring) == 0);
    verify(teststr.FindI(&acteststring[10]) == 10);

    verify(teststr.FindI(lesserstring) == KNotFound);
}

test("Find first of (C16)")
{
    OpStringC teststr(UNI_L("ABCDEFGHIJKLMNOPQRSTUVWXYZ"));
    verify(teststr.FindFirstOf('A') == 0);
    verify(teststr.FindFirstOf('K') == 10);
    verify(teststr.FindFirstOf('Z') == 25);
    verify(teststr.FindFirstOf('*') == KNotFound);
}

test("Find first of (C8)")
{
    OpStringC8 teststr("ABCDEFGHIJKLMNOPQRSTUVWXYZ");
    verify(teststr.FindFirstOf('A') == 0);
    verify(teststr.FindFirstOf('K') == 10);
    verify(teststr.FindFirstOf('Z') == 25);
    verify(teststr.FindFirstOf('*') == KNotFound);
}

test("Find last of (C16)")
{
    OpStringC teststr(UNI_L("AIBOHPHOBIA"));

    verify(teststr.FindLastOf('A') == 10);
    verify(teststr.FindLastOf('P') == 5);
    verify(teststr.FindLastOf('*') == KNotFound);
    verify(teststr.FindLastOf('\00') == teststr.Length());
}

test("Find last of (C8)")
{
    OpStringC8 teststr("AIBOHPHOBIA");

    verify(teststr.FindLastOf('A') == 10);
    verify(teststr.FindLastOf('P') == 5);
    verify(teststr.FindLastOf('*') == KNotFound);
    verify(teststr.FindLastOf('\00') == teststr.Length());
}


test("Index (S)")
{
    OpStringS teststr; teststr.Set(teststring);
    for (int i = 0; i < testlen; i++)
	verify(teststr[i] == teststring[i]);
}


test( "Index (S)" )
{
    OpStringS teststr; teststr.Set(teststring);
    for (int i = 0; i < testlen; i++)
      verify(teststr[i] == teststring[i]);
}

test( "Length (S)" )
{
    OpStringS teststr; teststr.Set(teststring);
    verify(teststr.Length() == testlen);
}

test( "Compare (S)" )
{
    OpStringS teststr; teststr.Set(teststring);
    OpStringS lesserstr; lesserstr.Set(lesserstring);
    OpStringS shortstr; shortstr.Set(shortstring);

    verify(teststr.Compare(teststring) == 0);
    verify(teststr.Compare(lesserstring) > 0);
    verify(lesserstr.Compare(teststring) < 0);
    verify(shortstr.Compare(lesserstring, 10) == 0);
    verify(teststr.Compare(shortstring) > 0);
    verify(shortstr.Compare(teststring) < 0);

    verify(teststr.Compare(teststr) == 0);
    verify(teststr.Compare(lesserstr) > 0);
    verify(lesserstr.Compare(teststr) < 0);
    verify(teststr.Compare(shortstr) > 0);
    verify(shortstr.Compare(teststr) < 0);
}

test( "Compare case insensitive (S)" )
	require initialization;
{
    OpStringS teststr; teststr.Set(teststring);
    OpStringS lesserstr; lesserstr.Set(lesserstring);
    OpStringS shortstr; shortstr.Set(shortstring);
    OpStringS acteststr; acteststr.Set(acteststring);
    OpStringS aclesserstr; aclesserstr.Set(aclesserstring);
    OpStringS acshortstr; acshortstr.Set(acshortstring);

    verify(teststr.CompareI(acteststring) == 0);
    verify(teststr.CompareI(aclesserstring) > 0);
    verify(lesserstr.CompareI(acteststring) < 0);
    verify(shortstr.CompareI(aclesserstring, 10) == 0);

    verify(teststr.CompareI(acshortstring) > 0);
    verify(shortstr.CompareI(acteststring) < 0);

    verify(teststr.CompareI(acteststr) == 0);
    verify(teststr.CompareI(aclesserstr) > 0);
    verify(lesserstr.CompareI(acteststr) < 0);
    verify(teststr.CompareI(acshortstr) > 0);
    verify(shortstr.CompareI(acteststr) < 0);
}

test("CStr (S)")
{
    OpStringS teststr; teststr.Set(teststring);
    verify( !TestUtils::compareStr( teststring, teststr.CStr() ) );
}

test("Find (S)")
{
    OpStringS teststr; teststr.Set(teststring);
    OpStringS lesserstr; lesserstr.Set(lesserstring);

    verify(teststr.Find(teststring) == 0);
    verify(teststr.Find(&teststring[10]) == 10);

    verify(teststr.Find(lesserstring) == KNotFound);
}

test("Find case insensitive (S)")
	require initialization;
{
    OpStringS teststr; teststr.Set(teststring);

    verify(teststr.FindI(acteststring) == 0);
    verify(teststr.FindI(&acteststring[10]) == 10);

    verify(teststr.FindI(lesserstring) == KNotFound);
}

test("Find first of (S)")
{
    OpStringS teststr; teststr.Set(UNI_L("ABCDEFGHIJKLMNOPQRSTUVWXYZ"));
    verify(teststr.FindFirstOf('A') == 0);
    verify(teststr.FindFirstOf('K') == 10);
    verify(teststr.FindFirstOf('Z') == 25);
    verify(teststr.FindFirstOf('*') == KNotFound);
}

test("Find last of (S)")
{
    OpStringS teststr; teststr.Set(UNI_L("AIBOHPHOBIA"));

    verify(teststr.FindLastOf('A') == 10);
    verify(teststr.FindLastOf('P') == 5);
    verify(teststr.FindLastOf('*') == KNotFound);
    verify(teststr.FindLastOf('\00') == teststr.Length());
}

test("Index (S)")
{
    OpStringS teststr; teststr.Set(teststring);
    for (int i = 0; i < testlen; i++)
		verify(teststr[i] == teststring[i]);
}

test("SpanOf")
	require UNUSED;
{
    OpStringC16 teststr16(UNI_L("abcdefghijklmnopqrstuvwxyz"));
	OpStringC8 teststr8("abcdefghijklmnopqrstuvwxyz");

	OpStringC16 abcdefghij_str16(UNI_L("abcdefghij"));
	OpStringC8 abcdefghij_str8("abcdefghij");

	OpStringC16 jihgfedcba_str16(UNI_L("jihgfedcba"));
	OpStringC8 jihgfedcba_str8("jihgfedcba");

	OpStringC16 klm_str16(UNI_L("klm"));
	OpStringC8 klm_str8("klm");

	OpStringC16 null_str16;
	OpStringC8 null_str8;

	verify(teststr16.SpanOf(abcdefghij_str16) == 10);
	verify(teststr16.SpanOf(jihgfedcba_str16) == 10);
	verify(teststr16.SpanOf(klm_str16) == 0);
	verify(abcdefghij_str16.SpanOf(teststr16) == 10);
	verify(teststr16.SpanOf(null_str16) == 0);
	verify(null_str16.SpanOf(teststr16) == 0);

	verify(teststr8.SpanOf(abcdefghij_str8) == 10);
	verify(teststr8.SpanOf(jihgfedcba_str8) == 10);
	verify(teststr8.SpanOf(klm_str8) == 0);
	verify(abcdefghij_str8.SpanOf(teststr8) == 10);
	verify(teststr8.SpanOf(null_str8) == 0);
	verify(null_str8.SpanOf(teststr8) == 0);
}

test("SpanNotOf")
	require UNUSED;
{
	OpStringC16 teststr16(UNI_L("abcdefghijklmnopqrstuvwxyz"));
	OpStringC8 teststr8("abcdefghijklmnopqrstuvwxyz");

	OpStringC16 abcdefghij_str16(UNI_L("abcdefghij"));
	OpStringC8 abcdefghij_str8("abcdefghij");

	OpStringC16 jihgfedcba_str16(UNI_L("jihgfedcba"));
	OpStringC8 jihgfedcba_str8("jihgfedcba");

	OpStringC16 klm_str16(UNI_L("klm"));
	OpStringC8 klm_str8("klm");

	OpStringC16 null_str16;
	OpStringC8 null_str8;

	verify(teststr16.SpanNotOf(abcdefghij_str16) == 0);
	verify(teststr16.SpanNotOf(jihgfedcba_str16) == 0);
	verify(teststr16.SpanNotOf(klm_str16) == 10);
	verify(abcdefghij_str16.SpanNotOf(teststr16) == 0);
	verify(teststr16.SpanNotOf(null_str16) == 26);
	verify(null_str16.SpanNotOf(teststr16) == 0);

	verify(teststr8.SpanNotOf(abcdefghij_str8) == 0);
	verify(teststr8.SpanNotOf(jihgfedcba_str8) == 0);
	verify(teststr8.SpanNotOf(klm_str8) == 10);
	verify(abcdefghij_str8.SpanNotOf(teststr8) == 0);
	verify(teststr8.SpanNotOf(null_str8) == 26);
	verify(null_str8.SpanNotOf(teststr8) == 0);
}

test("SetUTF8FromUTF16")
{
	const uni_char *source = UNI_L("\0x3000\0x3001\0x3002\0x3003");
	OpString8 utf8;
	OP_STATUS rc = utf8.SetUTF8FromUTF16(source);
	verify(OpStatus::IsSuccess(rc));
	verify(utf8.Compare("\0xe3\0x80\0x80\0xe3\0x80\0x81\0xe3\0x80\0x82\0xe3\0x80\0x83") == 0);
}

test("Append swedish chars")
{
	OpString s;
	s.Append("\345\344\366"); // "едц"
	verify(s.Compare(UNI_L("\345\344\366")) == 0);
}

test("Insert swedish chars")
{
	OpString s;
	s.Insert(0, "\345\344\366");
	verify(s.Compare(UNI_L("\345\344\366")) == 0);
}

test("Set swedish chars")
{
	OpString s;
	s.Set("\345\344\366");
	verify(s.Compare(UNI_L("\345\344\366")) == 0);

	OpString8 s8;
	s8.Set(UNI_L("\345\344\366"));
	verify(s8.Compare("\345\344\366") == 0);
}

test("OpString8::AppendFormat")
{
	OpString8 s;
	s.AppendFormat("%s %d", "test", 1);
	verify(op_strcmp(s.CStr(), "test 1") == 0);

	char buf[101]; /* ARRAY OK 2009-04-03 adame */
	int i;
	for (i=0; i < 101; ++i)
		buf[i] = 'A';
	buf[100] = 0;
	s.AppendFormat("%s", buf);
	verify(op_strncmp(s.CStr(), "test 1", 6) == 0);
	for (i=0; i < 100; ++i)
	{
		verify(*(char*)(s.CStr() + i + 6) == 'A');
	}
}

test("OpString16::AppendFormat")
{
	OpString s;
	s.AppendFormat(UNI_L("%s %d"), UNI_L("test"), 1);
	verify(uni_strcmp(s.CStr(), UNI_L("test 1")) == 0);

	uni_char buf[101]; /* ARRAY OK 2009-04-03 adame */
	int i;
	for (i=0; i < 101; ++i)
		buf[i] = 'A';
	buf[100] = 0;
	s.AppendFormat(UNI_L("%s"), buf);
	verify(uni_strncmp(s.CStr(), UNI_L("test 1"), 6) == 0);
	for (i=0; i < 100; ++i)
	{
		verify(*(uni_char*)(s.CStr() + i + 6) == 'A');
	}
}

test("Compare NULL and empty string")
{
	OpStringC8 str("");
	verify(str.Compare(NULL) == 0);
	verify(str.Compare("") == 0);

	OpStringC8 str2(NULL);
	verify(str2.Compare(NULL) == 0);
	verify(str2.Compare("") == 0);

	OpStringC str3(UNI_L(""));
	verify(str3.Compare((uni_char*)NULL) == 0);
	verify(str3.Compare(UNI_L("")) == 0);
	verify(str3.Compare((char*)NULL) == 0);
	verify(str3.Compare("") == 0);

	OpStringC str4((uni_char*)NULL);
	verify(str4.Compare((uni_char*)NULL) == 0);
	verify(str4.Compare(UNI_L("")) == 0);
	verify(str4.Compare((char*)NULL) == 0);
	verify(str4.Compare("") == 0);

	OpString8 str5;
	str5.Set("");
	verify(str5.Compare(NULL) == 0);
	verify(str5.Compare("") == 0);

	OpString8 str6;
	str6.Set((char*)NULL);
	verify(str6.Compare(NULL) == 0);
	verify(str6.Compare("") == 0);

	OpString str7;
	str7.Set(UNI_L(""));
	verify(str7.Compare((uni_char*)NULL) == 0);
	verify(str7.Compare(UNI_L("")) == 0);
	verify(str7.Compare((char*)NULL) == 0);
	verify(str7.Compare("") == 0);

	OpString str8;
	str8.Set((uni_char*)NULL);
	verify(str8.Compare((uni_char*)NULL) == 0);
	verify(str8.Compare(UNI_L("")) == 0);
	verify(str8.Compare((char*)NULL) == 0);
	verify(str8.Compare("") == 0);
}

test("SubString")
{
	OpString8 str1;
	OpString16 str2;

	str1.Set("foobar");
	verify(str1.SubString(3).Compare("bar") == 0);

	str2.Set("foobar");
	verify(str2.SubString(3).Compare("bar") == 0);
}

test("Strip")
{
	OpString8 str;

	str.Set(" \na \t\nbc \t \n");
	str.Strip(FALSE, FALSE);
	verify(str.Compare(" \na \t\nbc \t \n") == 0);

	str.Set(" \na \t\nbc \t \n");
	str.Strip(TRUE, FALSE);
	verify(str.Compare("a \t\nbc \t \n") == 0);

	str.Set(" \na \t\nbc \t \n");
	str.Strip(FALSE, TRUE);
	verify(str.Compare(" \na \t\nbc") == 0);

	str.Set(" \na \t\nbc \t \n");
	str.Strip(TRUE, TRUE);
	verify(str.Compare("a \t\nbc") == 0);
}

test("SetConcat")
{
	OpString8 str;

	str.SetConcat("foo", "bar");
	verify(str.Compare("foobar") == 0);

	str.SetConcat("foo", "bar", "fum");
	verify(str.Compare("foobarfum") == 0);

	str.SetConcat("foo", "bar", "fum", "fie");
	verify(str.Compare("foobarfumfie") == 0);

	str.SetConcat("foo", "bar", "fum", "fie", "foo2", "bar2", "fum2", "fie2");
	verify(str.Compare("foobarfumfiefoo2bar2fum2fie2") == 0);
}

test("Insert") leakcheck;
{
	OpString16 str1;
	OpString8 str2;

	str1.Set("foofum");
	str1.Insert(3, "bar");
	verify(str1.Compare("foobarfum") == 0);

	str1.Insert(0, "");

	str2.Set("foofum");
	str2.Insert(3, "bar");
	verify(str2.Compare("foobarfum") == 0);

	str2.Insert(0, "");
}

test("SpanOf")
{
	int ret;
	OpStringC16 str(UNI_L("123FooBar0123Fum9"));
	OpStringC8 str2("123FooBar0123Fum9");

	ret = str.SpanOf(UNI_L("0123456789F"));
	verify(ret == 4);

	ret = str.SpanOf(UNI_L("23Foo"));
	verify(ret == 0);

	ret = str.SpanOf(UNI_L("4567891"));
	verify(ret == 1);

	ret = str.SpanOf(UNI_L(""));
	verify(ret == 0);

	ret = str2.SpanOf("0123456789F");
	verify(ret == 4);

	ret = str2.SpanOf("23Foo");
	verify(ret == 0);

	ret = str2.SpanOf("4567891");
	verify(ret == 1);

	ret = str2.SpanOf("");
	verify(ret == 0);

}

test("SpanNotOf")
{
	int ret;
	OpStringC8 str("FooBar0123Fum9");

	ret = str.SpanNotOf("0123456789");
	verify(ret == 6);

	ret = str.SpanNotOf("9");
	verify(ret == 13);

	ret = str.SpanNotOf("F");
	verify(ret == 0);

	ret = str.SpanNotOf("");
	verify(ret == 14);
}

test("FindFirstOf")
{
	int ret;
	OpStringC8 str("FooBar0123Fum9");
	OpStringC16 str2(UNI_L("FooBar0123Fum9"));

	ret = str.FindFirstOf("0123456789");
	verify(ret == 6);

	ret = str.FindFirstOf("9");
	verify(ret == 13);

	ret = str.FindFirstOf("F");
	verify(ret == 0);

	ret = str.FindFirstOf("");
	verify(ret == KNotFound);	

	ret = str2.FindFirstOf(UNI_L("0123456789"));
	verify(ret == 6);

	ret = str2.FindFirstOf(UNI_L("9"));
	verify(ret == 13);

	ret = str2.FindFirstOf(UNI_L("F"));
	verify(ret == 0);

	ret = str2.FindFirstOf(UNI_L(""));
	verify(ret == KNotFound);	
}

test("MakeUpper")
{
	OpString8 str1;
	OpString16 str2;

	str1.Set("FoObaR,.-");
	str1.MakeUpper();
	verify(str1.Compare("FOOBAR,.-") == 0);

	str2.Set("FoObaR,.-\345\344\366");
	str2.MakeUpper();
	verify(str2.Compare("FOOBAR,.-\305\304\326") == 0);

	str1.Set("");
	str1.MakeUpper();
	verify(str1.Compare("") == 0);

	str2.Set("");
	str2.MakeUpper();
	verify(str2.Compare("") == 0);
}

test("FindI")
{
	int ret;
	OpStringC16 str(UNI_L("FooBar"));
	OpStringC16 str2(UNI_L("FooBar"));

	ret = str.FindI("Foo");
	verify(ret == 0);

	ret = str.FindI("Ba");
	verify(ret == 3);

	ret = str.FindI("");
	verify(ret == 0);

	ret = str.FindI("foobarfum");
	verify(ret == KNotFound);

	ret = str2.FindI("Foo");
	verify(ret == 0);

	ret = str2.FindI("Ba");
	verify(ret == 3);

	ret = str2.FindI("");
	verify(ret == 0);

	ret = str2.FindI("foobarfum");
	verify(ret == KNotFound);
}

test("Wipe")
{
	OpString16 str;

	str.Set(UNI_L("foo"));
	int old_capacity = str.Capacity();
	str.Wipe();
	verify_string(str, "");
	verify(str.Capacity() == old_capacity);

	OpString8 str2;

	str2.Set(UNI_L("foo"));
	int old_capacity2 = str2.Capacity();
	str2.Wipe();
	verify_string(str2, "");
	verify(str2.Capacity() == old_capacity2);
}

test("Append")
{
	OpString8 str;

	str.Set("");
	str.AppendL("foo");
	verify(str.Compare("foo") == 0);

	str.AppendL("bar");
	verify(str.Compare("foobar") == 0);

	str.AppendL("");
	verify(str.Compare("foobar") == 0);
}

test("OpString8::Set from self")
{
	OpString8 str1, str2, str3;

	str1.Set("ABC");
	str1.Set(str1, 2);
	verify_string(str1.CStr(), "AB");

	str2.Set("ABC");
	str2.Set(str2, 4);
	verify_string(str2.CStr(), "ABC");

	str3.Set("ABCDEFG");
	str3.Set(str3, 8);
	verify_string(str3.CStr(), "ABCDEFG");
}

test("OpString::Set from self")
{
	OpString str1, str2, str3;

	str1.Set("ABC");
	str1.Set(str1, 2);
	verify_string(str1.CStr(), UNI_L("AB"));

	str2.Set("ABC");
	str2.Set(str2, 4);
	verify_string(str2.CStr(), UNI_L("ABC"));

	str3.Set("ABCDEFG");
	str3.Set(str3, 8);
	verify_string(str3.CStr(), UNI_L("ABCDEFG"));
}

test("OpString8 <-> OpData conversion")
{
	OpString8 s;
	OpData d;
	verify(s.Length() == 0);
	verify_success(s.AppendToOpData(d));
	verify(d.Length() == 0);
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromOpData(d));
	verify(s.Length() == 0);

	verify_success(s.Set("foo bar"));
	verify(s.Length() == 7);
	verify(s.Compare("foo bar") == 0);
	verify_success(s.AppendToOpData(d));
	verify(d.Length() == 7);
	verify(d == "foo bar");
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromOpData(d));
	verify(s.Length() == 7);
	verify_string(s, d);

#define test_string "Lorem ipsum dolor sit amet, consectetur adipiscing elit. In fringilla, dolor eget faucibus tincidunt, ante metus commodo sapien, et varius sem eros non dui. Nullam ultricies eros vel nisl turpis duis."
	verify_success(s.Set(test_string));
	verify(s.Length() == (int) op_strlen(test_string));
	verify(s.Compare(test_string) == 0);
	d.Clear();
	verify_success(s.AppendToOpData(d));
	verify(d.Length() == op_strlen(test_string));
	verify(d == test_string);
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromOpData(d));
	verify(s.Length() == (int) op_strlen(test_string));
	verify_string(s, d);
#undef test_string
}

test("OpString <-> UniString conversion")
{
	OpString s;
	UniString d;
	verify(s.Length() == 0);
	verify_success(s.AppendToUniString(d));
	verify(d.Length() == 0);
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromUniString(d));
	verify(s.Length() == 0);

	verify_success(s.Set(UNI_L("foo bar")));
	verify(s.Length() == 7);
	verify(s.Compare(UNI_L("foo bar")) == 0);
	verify_success(s.AppendToUniString(d));
	verify(d.Length() == 7);
	verify(d == UNI_L("foo bar"));
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromUniString(d));
	verify(s.Length() == 7);
	verify_string(s, d);

#define test_string UNI_L("Lorem ipsum dolor sit amet, consectetur adipiscing elit. In fringilla, dolor eget faucibus tincidunt, ante metus commodo sapien, et varius sem eros non dui. Nullam ultricies eros vel nisl turpis duis.")
	verify_success(s.Set(test_string));
	verify(s.Length() == (int) uni_strlen(test_string));
	verify(s.Compare(test_string) == 0);
	d.Clear();
	verify_success(s.AppendToUniString(d));
	verify(d.Length() == uni_strlen(test_string));
	verify(d == test_string);
	s.Empty();
	verify(s.Length() == 0);
	verify_success(s.SetFromUniString(d));
	verify(s.Length() == (int) uni_strlen(test_string));
	verify_string(s, d);
#undef test_string
}
