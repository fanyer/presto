/* -*- Mode: c++; tab-width: 4; c-basic-offset: 4 -*-
 *
 * Copyright (C) 2002-2007 Opera Software AS.  All rights reserved.
 *
 * This file is part of the Opera web browser.
 * It may not be distributed under any circumstances.
 *
 * This file cointains tests to test the test-system, not _really_
 * examples of how you should write tests. More or less all features
 * are used in this file, though.
 */

// Test include expressions.
// None of these are supposed to include anything. :-)

// Group name, shown to the one running the tests.
group "selftest.basic";

import "selftest.tables";

// Check conditional disabled.
disabled if(false);
disabled if(ST_CAP_NOT_REALLY);
disabled if(!true);


include if(exists) "no_such_file";
include exists "no_such_file";
include exists && defined(NO_SUCH_DEFINE) "no_such_file";
include if(NO_SUCH_DEFINE) "no_such_file";
include if(defined(NO_SUCH_DEFINE)) "no_such_file";
include undefined(SELFTEST) "no_such_file";
include defined NO_SUCH_DEFINE "no_such_file";
include if((((((defined(NO_SUCH_DEFINE))))))) "no_such_file";
include {if(defined(NO_SUCH_DEFINE))} "no_such_file";
include {
	exists
	{
		if(NO_SUCH_DEFINE)
	}
} "no_such_file";


test("#include")
{
#  include "include_test.txt"
	verify( 1 == 0 );
}

test( "#string" )
{
	const char given[] =
#string "include_test.txt"
		;
	verify( !op_strncmp(given, "if( 0 )", 4) );
}

test("Verify")
{
	if (TRUE)
		verify(TRUE);
	else
		verify(FALSE);
}

test("Verify 2.1")
{
	verify(TRUE,
		my lovely error message
		that spans
		multiple lines);
	verify(TRUE, "my lovely error message");
}

test("Verify 2.2") fails;
{
	verify(FALSE, "my lovely error message");
}

test("Verify 2.3") fails;
{
	verify(FALSE, my lovely error message
			that spans
			multiple lines);
}

test("Finally, part 1")
{
	ok = 0;
}
finally
{
	ok = 1;
}

test("Finally, part 2")
{
	int ok = 0;
	verify( this->ok != ok );
}
finally
{
	ok = 0; // Should not set this->ok
}

test("finally, part 3")
{
	verify( 1==1 );
	verify( 1==1 );
	verify( 1==1 );
	verify( 1==1 );
}
finally
{
	ok <<= 1;
}

test("Previous bug #1")
{
	verify( 2 == ok );
	verify( 1 <= ok );
	verify( 1 < ok );
	verify( 4 > ok );
	verify( 3 >= ok );
	verify( 3 != ok );

	verify( ok == 2 );
	verify( ok >= 1 );
	verify( ok > 1 );
	verify( ok < 4 );
	verify( ok <= 3 );
	verify( ok != 3 );
}

test("Finally, part 3")
{
	verify( ok == 2 );
}

test("Big ints")
	require HAVE_LONGLONG;
{
	/* These tests make sure that the the generated code
	   for expressions like "int op int", which has custom
	   handling to output the result in a more verbose way,
	   does not break the test with big ints. */
	verify(-0x1000000000LL == -0x1000000000LL);
	verify( 0x1000000000LL ==  0x1000000000LL);

	verify(-0x1000000000LL !=  0x1000000000LL);
	verify(-0x1000000000LL <   0x1000000000LL);

	verify( 0x1000000000LL != -0x1000000000LL);
	verify( 0x1000000000LL >  -0x1000000000LL);
}

test("Expression evaluation")
{
	/* This test makes sure each operand in verify
	   expressions is evaluated only once. */
	struct E { int calls, value; int x(){ calls++; return value;}} e = {0, 1};

	verify(e.x());
	verify(!!e.x());
	verify(e.x() != (e.x() + 1));
	verify(e.x() < (e.x() + 1));
	verify(e.x() == e.x());
	verify(e.x() == 1);
	verify(1 == e.x());
	verify(e.x() > 0);
	verify(0 < e.x());

	verify(e.calls == 12);
}

test("Delay")
	delay pre 0.2;
	delay post 0.2;
{
	verify(1==1);
}

test("Fail")
	fails; // Indicates that this test should fail.
    leakcheck;
{
	verify( 1 == 0 );
}

test("Disabled")
	disabled; // This test should not be run at all.
{
	verify( 1 == 0 );
}

test("repeat")
  repeat 10000;
  leakcheck;
{
  ++repeat_count;
}

test("require")
  require NO_SUCH_DEFINE;
{
  verify( 0 == 1 );
}

test("require, part 2")
  require SELFTEST;
  require undefined NO_SUCH_DEFINE;
{
  verify( 1 == 1 );
}

test("require, part 3")
  require undefined SELFTEST;
{
  verify( 1 == 0 );
}

test( "require, part 4")
  require failure "require";
  require success "require, part 2";
  require failure "require, part 3";
{
  verify( 1 == 1 );
}

test( "repeat, part 2" )
  require success "repeat";
{
  verify( repeat_count == 10000 );
}

test( "require -> skipped" )
  require failure "repeat*";
{
  verify( repeat_count == 10000 );
}


test_equal ("multi-line string (sizeof)", (unsigned int)sizeof (multi_string), (unsigned int)op_strlen(multi_string)+1);


test_equal( "//! strings",
			//! String number one
			//  This is a comment
			//! String continues.
			,
			"String number one\nString continues.\n");

test_equal( "//! strings 2",
			//!  String number one
			//  This is a comment
			//!  String continues.
			,
			" String number one\n String continues.\n");

// foreach generates many tests from one test.
// The syntax is
//
// foreach VARIABLE in LIST { code }
//
// VARIABLE can be a list. If so, each item in LIST has to be a list.
// In the list, you can use value1:value2. value1 is used to replace
// all identifiers named with the same name as the variable, value2 is
// used to replace all occurences of $(VARIABLE). The latter is done
// even in strings.

// Simple variable.
foreach (Whatever) from { 1:"one", 2:"two", 3:"three", 4, 5, 6:"six", 7, 8, 9:"nine", 10 }
{
  test( "Testing that $(Whatever) is true" )
  {
    verify( Whatever );
  }
}

// Multiple variables
foreach ( Type, Type2 ) from { {Foo:"Foo" , /* C-style comment } */    Bar},
  {Bar,           Foo:"Foo"} // And a C++ style comments. } <-- testing parser
}
{
  test( "Testing class $(Type) / $(Type2)" )
  {
    Type var;
    Type2 var2;
    switch( var.test() )
    {
    case 10:
      verify( var2.test() == 20 );
      break;
    case 20:
      verify( var2.test() == 10 );
      break;
    }
  }
}


test_equal( "Equal simple char*",    "Foo"+1,         "oo" );
test_equal( "Equal simple uni_char*", UNI_L("Foo")+1, UNI_L("oo") );

exit {
	delete m_timer;
	delete m_timer_list;
}

setup {
	repeat_count = 0;
	ok = 0;
	m_timer = 0;
	m_timer_list = 0;
}

include "modules/hardcore/timer/optimer.h";


global
{
  static const char table_number_one[] = { 1, 2, 3, 4, 5, 6, 7, 8 };
  static const char table_number_two[] = { 1, 2, 3, 4, 5, 6, 7, 8 };

  static const char table_number_three[][2] = { {1, 2} };

  static const char multi_string[] =
	"aaaaaaaaaa"
	"bbbbb"
	"ccccccccccccccc"
	"1";

  int repeat_count, ok;

  class Foo
  {
  public:
    int test() { return 10; }
  };

  class Bar
  {
  public:
    int test() { return 20; }
  };

  class Gazonk
  {
  protected:
	  Gazonk() {}
  public:
	  static Gazonk *Construct();
	  int test();
  };

  class Elefant
  {
  protected:
	  Elefant() {}
  public:
	  static Elefant *Construct();
	  int test();
  };

  int Gazonk::test() {
	  return 30;
  }

  Gazonk *Gazonk::Construct() {
	  return new Gazonk;
  }

  int Elefant::test() {
	  return 42;
  }

  Elefant *Elefant::Construct() {
	  return new Elefant;
  }

  class myTL : public OpTimerListener
  {
  public:
	myTL() : has_been_called(FALSE){}

	virtual void OnTimeOut( OpTimer* /*timer*/ ) {
		// All OK.
		if (!has_been_called)
			ST_passed();
		has_been_called = TRUE;
	}
	BOOL has_been_called;
  };

  OpTimer *m_timer;
  myTL * m_timer_list;
}

test("Checking class in class")
{
	OpAutoPtr<Gazonk> foo(Gazonk::Construct( ));
	verify( foo->test() == 30 );
}

test("Checking class in class 2")
{
	OpAutoPtr<Elefant> foo(Elefant::Construct( ));
	verify( foo->test() == 42 );
}



test( "async simple" )
	async;
{
	ST_passed();
}

test( "async with timer" )
	async;
	timer;
{
	m_timer = new OpTimer();
	if (!m_timer)
	{
		ST_failed( "Failed to create timer" );
	}
	else
	{
		m_timer_list = new myTL; // in the globals section.
		if( m_timer_list )
		{
			m_timer->SetTimerListener( m_timer_list );
			m_timer->Start( 40 );
		}
		else
			ST_failed( "Failed to create timer listener" );
	}
}
finally
{
	/*
	 * this has to be commented for now
	if (m_timer_list && !m_timer_list->has_been_called)
	{
		ST_failed( "finally block called before the test completed !!!" );
		m_timer_list->has_been_called = true;
	}
	*/
}

test( "manual user" )
	manual "Are you there?";
{
}

test( "async simple ecma" )
	language ecmascript;
    async;
{
	ST_passed();
}


test( "async with timer ecma" )
	language ecmascript;
    async;
{
	// One wonders what happens if the timeout could not be created
	// here. :-)

	setTimeout( ST_passed, 10 );
}


test("async many callbacks cancel")
	language ecmascript;
	async;
{
	function foo(){
		ST_failed("This javascript code should not run!", "basic.ot", 445);
	}
	var bar = function (){
		return foo();
	}
	var baz = function (){
		ST_passed();
	}
	setTimeout(function(){(function(){
		baz.call(null);
	})();}, 10 );
	setTimeout(function(){(function(){
		bar.call(null);
	})();}, 20 );
}

test("async many callbacks cancel leftover")
	language ecmascript;
	allowexceptions;
	async;
{
	//should not be failed because of the previous test's leftover timeout
	setTimeout(function(){throw new Error("This exception should not cause the selftest to fail");},10);
	setTimeout(function(){ST_failed("timed out ! ", "basic.ot", 468);},200);
	setTimeout(function(){
		ST_passed();
	}, 100);
}

// Group 2 in the same file.

html { "<html id=bad>" }

group "selftest.basic2";
language ecmascript;

test("True")
{
	verify( 1 == 1 );
}

test("Document was reset")
{
	// Document must be reset to an empty document between groups,
	// so scripts don't leak through or state does not affect the
	// next group of tests. The document looks like:
	//  <html><head></head><body></body></html>
	verify(!document.getElementById('bad'));
	verify(document.documentElement);
	verify(document.head);
	verify(document.body);
	verify(!document.body.firstChild);
}

// Test the HTML construct, and strings in comments.
html
{
	//!   <html>
	//!   <body bgcolor='white' text='black'>
	//!   <h1>Simple HTML test</h1>
	//!
	//!   <p>This is the first paragraph. Does it work, I wonder?
	//!   This syntax is easier to use than strings, since you can easily
	//!   write newlines, "quotes" of 'all' kinds, \-characters etc.</p>
	//!
	//!   <p>
	//!   This should be another paragraph.
	//!   </p>
	//!   </body>
	//!   </html>
}

language c++;
language c;
language c++;

test( "Checking contents" )
	require initialization;
{
	verify( state.doc );
	verify( find_element( "html" ));
	verify( find_element( "body" ));
	verify( find_element( "h1" )  );
	verify( find_element( "p",1 ) );
	verify( find_element( "p",2 ) );
	verify( find_element( "p",1 ) != find_element( "p",2 ) );
}


data "text/html" {
	//! <html><body><h1>Simple HTML.</h1></body></html>
}

language ecmascript;

text {
	//! <html><body><h1>text/plain</h1></body></html>
}

xml {
	//! <html><body><h1>text/xml</h1></body></html>
}

svg {
	//! <svg></svg>
}

data "image/gif" {
	//! An image. :-)
}

html "";
html { "" };
html "http://example.org/catz" "<html>";
html "http://example.org/catz" {
	//! <html id="helloworld"><body>
};

//  Same blocks as before but without trailing semi-colon, plus extra onload listener.
html ""
html { "" }
html "http://example.org/catz" "<html>"
html "http://example.org/catz" {
	//! <html id="helloworld"><body onload="window.onload_ran=1;">
}

test("Check html url 1")
{
	verify(window.onload_ran);
	verify(document.getElementById('helloworld'));
	verify(location.href == "http://example.org/catz");
}

test("False")
	fails;
{
	verify( 0 == 1 );
}

test("Simple")
{
	verify( 10 - 9 == 1 );
}

test("With reason")
{
	verify( true, "this cannot fail !");
}

xml { "<xml>XML documents with parse errors may not stall." }

test("xml malformed 1")
{
	// The document must have been replaced by the xml parsing failed page.
	verify(!document.getElementsByTagName('xml')[0]);
}

xml {
	//! <?xml version="1.0" encoding="utf-8"?>
	//! <xml>XML documents with encoding errors may not stall жи Єт</xml>
}

test("xml malformed 2")
{
	// The document must have been replaced by the xml parsing failed page.
	// If this fails, then perhaps, you might have made the xml fragment
	// above valid.
	verify(!document.getElementsByTagName('xml')[0]);
}

language c++;

global {
	OP_STATUS GetOK() const { return OpStatus::OK; }
	OP_STATUS GetERR() const { return OpStatus::ERR; }
	OP_STATUS GetERRMem() const { return OpStatus::ERR_NO_MEMORY; }
}

test("Test verify_status 1")
{
	verify_status(OpStatus::OK, GetOK());
	verify_status(OpStatus::ERR, GetERR(), "reason");
	verify_status(OpStatus::ERR_NO_MEMORY, GetERRMem());
}

test("Test verify_status 2.1") fails;
{
	verify_status(OpStatus::OK, GetERR(), "reason");
}

test("Test verify_status 2.2") fails;
{
	verify_status(OpStatus::ERR, GetERR());
	verify_status(OpStatus::ERR, GetERRMem());
}

test("Test verify_success 1")
{
	verify_success(OpStatus::OK);
	verify_success(GetOK(), "reason");
}

test("Test verify_success 2") fails;
{
	verify_success(GetERR(), "reason");
}

test("Test verify_success 2.1") fails;
{
	verify_success(OpStatus::ERR_NO_MEMORY);
}

test("Test verify_not_oom 1")
{
	int x;
	int *p = &x;
	verify_not_oom(p);
}

test("Test verify_not_oom 2.1") fails;
{
	int* x = NULL;
	verify_not_oom(x);
}

test("Test verify_not_oom 2.2") fails;
{
	verify_not_oom(NULL);
}

test("Test cpp verify_strings 1")
{
	verify_string(UNI_L("1"),UNI_L("1"));
	verify_string(UNI_L("1"),"1");
	verify_string("1",UNI_L("1"));
	verify_string("1","1");
	verify_string("1","1", "reason why it fails");
	verify_string(NULL, NULL);
	verify_string(static_cast<uni_char*>(NULL), static_cast<uni_char*>(NULL));
	verify_string(static_cast<uni_char*>(NULL), NULL);
}

test("Test cpp verify_strings 2.1") fails;
{
	verify_string(UNI_L("1"),UNI_L("2"));
}

test("Test cpp verify_strings 2.2") fails;
{
	verify_string("1","2");
}

test("Test cpp verify_strings 2.3") fails;
{
	verify_string(NULL,"2", "reason why it fails");
}

test("Test cpp verify_strings 3")
{
	OpString8 a;
	OpString b;
	OpData c;
	UniString d;
	verify_success(a.Set("foo"));
	verify_success(b.Set(UNI_L("foo")));
	verify_success(c.SetConstData("foo"));
	verify_success(d.SetConstData(UNI_L("foo")));

	verify_string("foo", a);
	verify_string("foo", b);
	verify_string("foo", c);
	verify_string("foo", d);

	verify_string(UNI_L("foo"), a);
	verify_string(UNI_L("foo"), b);
	verify_string(UNI_L("foo"), c);
	verify_string(UNI_L("foo"), d);

	verify_string(a, "foo");
	verify_string(a, UNI_L("foo"));
	verify_string(a, a);
	verify_string(a, b);
	verify_string(a, c);
	verify_string(a, d);

	verify_string(b, "foo");
	verify_string(b, UNI_L("foo"));
	verify_string(b, a);
	verify_string(b, b);
	verify_string(b, c);
	verify_string(b, d);

	verify_string(c, "foo");
	verify_string(c, UNI_L("foo"));
	verify_string(c, a);
	verify_string(c, b);
	verify_string(c, c);
	verify_string(c, d);

	verify_string(d, "foo");
	verify_string(d, UNI_L("foo"));
	verify_string(d, a);
	verify_string(d, b);
	verify_string(d, c);
	verify_string(d, d);
}

test("Test cpp verify_strings 4.1") fails;
{
	OpString8 a;
	a.Set("foo");
	verify_string(a, "bar");
}

test("Test cpp verify_strings 4.2") fails;
{
	OpString8 a;
	a.Set("foo");
	verify_string("bar", a);
}

test("Test cpp verify_strings 4.3") fails;
{
	OpString8 a;
	a.Set("foo");
	verify_string(a, UNI_L("bar"));
}

test("Test cpp verify_strings 4.4") fails;
{
	OpString8 a;
	a.Set("foo");
	verify_string(UNI_L("bar"), a);
}

test("Test cpp verify_strings 4.5") fails;
{
	OpString8 a;
	OpString8 b;
	a.Set("foo");
	b.Set("bar");
	verify_string(a, b);
}

test("Test cpp verify_strings 4.6") fails;
{
	OpString8 a;
	OpString8 b;
	a.Set("foo");
	b.Set("bar");
	verify_string(b, a);
}

test("Test cpp verify_strings 4.7") fails;
{
	OpString8 a;
	OpString b;
	a.Set("foo");
	b.Set(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.8") fails;
{
	OpString8 a;
	OpString b;
	a.Set("foo");
	b.Set(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 4.9") fails;
{
	OpString8 a;
	OpData b;
	a.Set("foo");
	b.SetConstData("bar");
	verify_string(a, b);
}

test("Test cpp verify_strings 4.10") fails;
{
	OpString8 a;
	OpData b;
	a.Set("foo");
	b.SetConstData("bar");
	verify_string(b, a);
}

test("Test cpp verify_strings 4.11") fails;
{
	OpString8 a;
	UniString b;
	a.Set("foo");
	b.SetConstData(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.12") fails;
{
	OpString8 a;
	UniString b;
	a.Set("foo");
	b.SetConstData(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 4.13") fails;
{
	OpString a;
	a.Set(UNI_L("foo"));
	verify_string(a, "bar");
}

test("Test cpp verify_strings 4.14") fails;
{
	OpString a;
	a.Set(UNI_L("foo"));
	verify_string("bar", a);
}

test("Test cpp verify_strings 4.15") fails;
{
	OpString a;
	a.Set(UNI_L("foo"));
	verify_string(a, UNI_L("bar"));
}

test("Test cpp verify_strings 4.16") fails;
{
	OpString a;
	a.Set(UNI_L("foo"));
	verify_string(UNI_L("bar"), a);
}

test("Test cpp verify_strings 4.17") fails;
{
	OpString a;
	OpString b;
	a.Set(UNI_L("foo"));
	b.Set(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.18") fails;
{
	OpString a;
	OpString b;
	a.Set(UNI_L("foo"));
	b.Set(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 4.19") fails;
{
	OpString a;
	OpData b;
	a.Set(UNI_L("foo"));
	b.SetConstData("bar");
	verify_string(a, b);
}

test("Test cpp verify_strings 4.20") fails;
{
	OpString a;
	OpData b;
	a.Set(UNI_L("foo"));
	b.SetConstData("bar");
	verify_string(b, a);
}

test("Test cpp verify_strings 4.21") fails;
{
	OpString a;
	UniString b;
	a.Set(UNI_L("foo"));
	b.SetConstData(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.22") fails;
{
	OpString a;
	UniString b;
	a.Set(UNI_L("foo"));
	b.SetConstData(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 4.23") fails;
{
	OpData a;
	a.SetConstData("foo");
	verify_string(a, "bar");
}

test("Test cpp verify_strings 4.24") fails;
{
	OpData a;
	a.SetConstData("foo");
	verify_string("bar", a);
}

test("Test cpp verify_strings 4.25") fails;
{
	OpData a;
	a.SetConstData("foo");
	verify_string(a, UNI_L("bar"));
}

test("Test cpp verify_strings 4.26") fails;
{
	OpData a;
	a.SetConstData("foo");
	verify_string(UNI_L("bar"), a);
}

test("Test cpp verify_strings 4.27") fails;
{
	OpData a;
	OpData b;
	a.SetConstData("foo");
	b.SetConstData("bar");
	verify_string(a, b);
}

test("Test cpp verify_strings 4.28") fails;
{
	OpData a;
	OpData b;
	a.SetConstData("foo");
	b.SetConstData("bar");
	verify_string(b, a);
}

test("Test cpp verify_strings 4.29") fails;
{
	OpData a;
	UniString b;
	a.SetConstData("foo");
	b.SetConstData(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.30") fails;
{
	OpData a;
	UniString b;
	a.SetConstData("foo");
	b.SetConstData(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 4.31") fails;
{
	UniString a;
	a.SetConstData(UNI_L("foo"));
	verify_string(a, "bar");
}

test("Test cpp verify_strings 4.32") fails;
{
	UniString a;
	a.SetConstData(UNI_L("foo"));
	verify_string("bar", a);
}

test("Test cpp verify_strings 4.33") fails;
{
	UniString a;
	a.SetConstData(UNI_L("foo"));
	verify_string(a, UNI_L("bar"));
}

test("Test cpp verify_strings 4.34") fails;
{
	UniString a;
	a.SetConstData(UNI_L("foo"));
	verify_string(UNI_L("bar"), a);
}

test("Test cpp verify_strings 4.35") fails;
{
	UniString a;
	UniString b;
	a.SetConstData(UNI_L("foo"));
	b.SetConstData(UNI_L("bar"));
	verify_string(a, b);
}

test("Test cpp verify_strings 4.36") fails;
{
	UniString a;
	UniString b;
	a.SetConstData(UNI_L("foo"));
	b.SetConstData(UNI_L("bar"));
	verify_string(b, a);
}

test("Test cpp verify_strings 5.1 - comparing empty strings")
{
	verify_string(UNI_L(""), OpStringC());
	verify_string(OpStringC(), UNI_L(""));
}

test("Test cpp verify_strings 5.2.1 - comparing empty strings") fails;
{
	verify_string(NULL, OpStringC());
}

test("Test cpp verify_strings 5.2.2 - comparing empty strings") fails;
{
	verify_string(OpStringC(), NULL);
}

test("Test cpp verify_strings 5.3 - comparing empty strings")
{
	OpString s;
	s.SetL(UNI_L(""));
	verify_string(s, s);
	verify_string(s, s.CStr());
	verify_string(s.CStr(), s);
}

subtest uninitialized_string_cstr_is_null()
{
	OpString s;
    verify(s.CStr() == NULL);
}

test("Test cpp verify_strings 5.4 - comparing empty strings") fails;
{
	// if this fails, it's ok, this test doesn't apply
	verify(uninitialized_string_cstr_is_null());

	// this fails because CStr() returns NULL, see the test above
	OpString s;
	verify_string(s, s.CStr());
}

test("has output")
	language ecmascript;
{
	verify(output);
	output();
}

group "selftest.missingsemicolons";

global
{
	// A missing semicolon produces compile errors which
	// are reported as being in code_snippets.cpp, so it's
	// not straightforward to understand there's a semicolon
	// missing. The compiler can just play happy and add
	// the leading semicolon
	int missing_leading_semicolon
}

setup
{
	missing_leading_semicolon = 0
}

subtest foo()
{
	missing_leading_semicolon++
}

test("check var"){
	verify(foo());
	verify(missing_leading_semicolon==1);
	missing_leading_semicolon++
}

finally
{
	missing_leading_semicolon--
}

exit
{
	missing_leading_semicolon = 0
}
